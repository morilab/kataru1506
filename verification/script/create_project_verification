#!/bin/csh -f
set TB              = "top"
set PJ_NAME         = "-"
set PJ_NUM          = "-"

##############################################################################
# 以下は原則として修正不要
##############################################################################
set CVSREP_OVM      = OVM/ovm-2.0.3
set CVSREP_TEMPLATE = HDL_template/OVM
set VERIDIR         = "./verification"
set CREATE_DATE     = `date -R`
set SYNTAX          = ${CVSREP_OVM:t}
set TOOL_VERSIONS   = `vsim -version`

# オプションの読み込み #######################################################
@ args = 1
set arg_num = $#argv
while($args <= $arg_num)
    switch ($argv[$args])
    case -tb:
        @ args += 1
        set TB = "$argv[$args]"
        breaksw
    case -dir:
        @ args += 1
        set VERIDIR = "$argv[$args]"
        breaksw
    case -pj_name:
        @ args += 1
        set PJ_NAME = "$argv[$args]"
        breaksw
    case -pj_num:
        @ args += 1
        set PJ_NUM = "$argv[$args]"
        breaksw
    default:
        goto Help
        breaksw
    endsw
    @ args += 1
end

# フォルダの作成 #############################################################
if(-de ${VERIDIR}) then
    echo "フォルダ[${VERIDIR}]は既に存在しています。処理を中断しました。"
    goto Error
else
    mkdir ${VERIDIR}
endif

# チェックアウト #############################################################
cvs -q export -D tomorrow -d ${VERIDIR}/script              ${CVSREP_TEMPLATE}/script
cvs -q export -D tomorrow -d ${VERIDIR}/sim                 ${CVSREP_TEMPLATE}/sim
cvs -q export -D tomorrow -d ${VERIDIR}/lib                 ${CVSREP_TEMPLATE}/lib
cvs -q export -D tomorrow -d ${VERIDIR}/lib/${CVSREP_OVM:t} ${CVSREP_OVM}
cvs -q export -D tomorrow -d ${VERIDIR}/tb/model/scoreboard ${CVSREP_TEMPLATE}/tb/model/scoreboard
cvs -q export -D tomorrow -d ${VERIDIR}/tb/model/crc        ${CVSREP_TEMPLATE}/tb/model/crc
cvs -q export -D tomorrow -d ${VERIDIR}/tb/verienv/${TB}    ${CVSREP_TEMPLATE}/tb/verienv/xxxx

set filepath = ${VERIDIR}/tb/verienv/${TB}
foreach oldfile ( `find ${filepath}` ${VERIDIR}/sim/go )
    if(-f ${oldfile}) then
        set newfile = `echo ${oldfile} | sed "s/xxxx/${TB}/"`
        if( ${oldfile} != ${newfile} ) then
            echo "${newfile:t}   \t(create from ${oldfile:t})"
        else
            set oldfile = ${oldfile}_$$
            echo "${newfile:t}"
            mv ${newfile} ${oldfile}
        endif
        sed ${oldfile} \
            -e "s/xxxx/${TB}/g" \
            -e "/^\/\/ Create Date *:/s/:/: ${CREATE_DATE}/" \
            -e "/^\/\/ Project Name *:/s/:/: ${PJ_NAME}/" \
            -e "/^\/\/ Project No. *:/s/:/: ${PJ_NUM}/" \
            -e "/^\/\/ Syntax *:/s/:.*/: ${SYNTAX}/" \
            -e "/^\/\/ Tool versions *:/s/:.*/: ${TOOL_VERSIONS}/" \
            -e "/setenv OVMHOME/s/lib.*/lib\/${CVSREP_OVM:t}/" \
            > ${newfile}
        \rm -r -f ${oldfile}
    endif
end
chmod +x ${VERIDIR}/sim/go

echo ""
echo "※ ${VERIDIR}/tb/verienv/${TB}/design.fに、RTLファイルの場所を追加してください。"
echo "   尚、相対パスで記述する場合は${VERIDIR}/simがカレントパスとなります。"
echo "   ${VERIDIR}/sim/dummy.vは不要ですので、環境の動作確認後、削除してください。"
echo ""

# 正常終了 ###################################################################
echo "環境構築が正常に終了しました。"
exit 0

# ヘルプ #####################################################################
Help:
set REV    = `echo '$Revision: 1.3 $'             | cut -d " " -f 2`
set UPDATE = `echo '$Date: 2012/05/29 06:40:44 $' | cut -d " " -f 2-3`

echo "OVM検証環境自動構築スクリプト rev${REV} (LastUpdate ${UPDATE})"
echo "<OPTIONS>"
echo "    -top {モジュール}   : 検証対象モジュール名"
echo "    -tb {検証環境名}    : 作成するテスト環境名(デフォルト:[${TB}])"
echo "    -dir {フォルダ}     : 検証環境出力フォルダ名(デフォルト:[${VERIDIR}])"
echo "    -pj_name {名前}     : ヘッダに記載するプロジェクト名(※半角推奨)"
echo "    -pj_num {番号}      : ヘッダに記載するプロジェクト番号(※半角推奨)"
exit 1

# 異常終了 ###################################################################
Error:
echo "       #######"
echo "       #"
echo "       #"
echo "       #         # ###   # ###   ###     # ###"
echo "       ######    ##      ##     #   #    ##"
echo "       #         #       #     #     #   #"
echo "       #         #       #     #     #   #"
echo "       #         #       #      #   #    #"
echo "       #######   #       #       ###     #"
echo ""
echo "環境構築に失敗しました。"
exit 1
